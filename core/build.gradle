plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'pojo-extension.java-conventions'
}

dependencies {
    implementation 'com.google.auto.service:auto-service:1.0'
    annotationProcessor 'com.google.auto.service:auto-service:1.0'

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.7.2'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.7.2'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.7.2'

    testImplementation group: 'org.jooq', name: 'joor-java-8', version: '0.9.14'

}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'pojo-extension'
            from components.java
            pom {
                name = 'Pojo Extension'
                description = 'Java Annotation Processor for Pojo Extension class'
                url = 'https://github.com/muehmar/pojo-extension'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/muehmar/pojo-extension/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'muehmar'
                        name = 'Martin MÃ¼hlemann'
                        email = 'poelsi@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/muehmar/pojo-extension.git'
                    developerConnection = 'scm:git:ssh://github.com/muehmar/pojo-extension.git'
                    url = 'https://github.com/muehmar/pojo-extension.git'
                }
            }
        }
    }
    repositories {
        maven {
            name = 'ossrh'
            credentials(PasswordCredentials)
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
